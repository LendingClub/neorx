apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: "jacoco"
apply plugin: "signing"

group 'io.macgyver.neorx'
version = "1.1.1"
configurations {
	//all*.exclude group: 'org.codehaus.groovy', module: 'groovy'
	all*.exclude group: 'log4j'
	all*.exclude group: 'xml-apis'
    all*.exclude group: 'commons-logging'
}

repositories {
   mavenLocal()
   mavenCentral()
   jcenter()
   

}

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {


	compile "io.reactivex:rxjava:${RXJAVA_VERSION}"
	compile "com.fasterxml.jackson.core:jackson-databind:${JACKSON_VERSION}"
	compile "com.squareup.okhttp:okhttp:${OKHTTP_VERSION}"
	compile "org.slf4j:slf4j-api:${SLF4J_VERSION}"
	
	
	testCompile 'org.assertj:assertj-core:2.2.0'  
	testCompile 'org.kohsuke:wordnet-random-name:1.2'  
	testCompile "junit:junit:${JUNIT_VERSION}"
	testCompile "com.squareup.okhttp:mockwebserver:${OKHTTP_VERSION}"
	testCompile "ch.qos.logback:logback-classic:${LOGBACK_VERSION}"
	testCompile "org.slf4j:jul-to-slf4j:${SLF4J_VERSION}"
	testCompile "com.google.guava:guava:18.0"  //  <<-- test scope only!
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
	
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
	
	
}

artifacts {
        archives sourcesJar, javadocJar
}
uploadArchives {
    repositories {
        mavenDeployer {
			
            name = 'macgyver-bintray'

			if (!project.has('BINRAY_USER')) {
				project.ext['BINTRAY_USER']=System.getenv("BINTRAY_USER")
			}
			if (!project.has('BINTRAY_KEY')) {
				project.ext['BINTRAY_KEY'] = System.getenv("BINTRAY_KEY")
			}
			
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			
            repository(id: 'macgyver-bintray', url: 'https://api.bintray.com/maven/robschoening/io-macgyver/neorx'){
                authentication(userName: BINTRAY_USER, password: BINTRAY_KEY)
			}
            pom.project {
				
				name 'NeoRx'
				description 'REST Client for Neo4j'
				url 'https://github.com/if6was9/NeoRx'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
                    }
                }
				scm {
					connection 'scm:git:git@github.com:if6was9/neorx.git'
					developerConnection 'scm:git:git@github.com:if6was9/neorx.git'
					url 'git@github.com:if6was9/neorx.git'
				}
            	developers {
					developer {
						name 'Rob Schoening'
						email 'robschoening@gmail.com'
					}
				}
            }
        }
    }
}

signing {
	sign configurations.archives
}

signArchives.onlyIf { 
	
	project.ext['signing.keyId'] = System.getenv("SIGNING_KEY_ID")
	project.ext['signing.password'] = System.getenv("SIGNING_PASSWORD")
	project.ext['signing.secretKeyRingFile'] = new File("./.gnupg/secring.gpg")
	
	def signingPropertiesSet = project.ext.has('signing.keyId') && project.ext['signing.keyId']
	if (!signingPropertiesSet) {
		println "properties required to sign artifacts not set -- disabling code signing"
	}
	else {
		new File("./.gnupg").mkdirs()
		ant.get(src: "${System.getenv("SIGNING_KEYRING_URL")}", dest: "./.gnupg/secring.gpg", verbose:false)
	}
	return signingPropertiesSet
}

jacocoTestReport {
    group = "reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }
    //classDirectories = fileTree(dir: 'build/classes/main', include: 'com/thc/**')
    //sourceDirectories = fileTree(dir: 'scr/java', include: 'com/thc/**')
    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
}

task (none) << {
	// dummy task to do nothing
}
