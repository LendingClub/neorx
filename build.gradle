apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: "jacoco"


group 'org.lendingclub.neorx'
version = "${NEORX_VERSION}"
configurations {
	//all*.exclude group: 'org.codehaus.groovy', module: 'groovy'
	all*.exclude group: 'log4j'
	all*.exclude group: 'xml-apis'
    all*.exclude group: 'commons-logging'
}

repositories {
   mavenLocal()
   mavenCentral()
   jcenter()
}



sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {


	compile "io.reactivex.rxjava2:rxjava:${RXJAVA_VERSION}"
	compile "org.slf4j:slf4j-api:${SLF4J_VERSION}"
	compile "org.neo4j.driver:neo4j-java-driver:$NEO4J_DRIVER_VERSION"
	compile "com.fasterxml.jackson.core:jackson-databind:${JACKSON_VERSION}"
	
	testCompile 'org.assertj:assertj-core:2.2.0'  
	testCompile 'org.kohsuke:wordnet-random-name:1.2'  
	testCompile "junit:junit:${JUNIT_VERSION}"
	
	testCompile "ch.qos.logback:logback-classic:${LOGBACK_VERSION}"
	testCompile "org.slf4j:jul-to-slf4j:${SLF4J_VERSION}"
	compile "com.google.guava:guava:21.0"  
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
	
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
	
	
}

artifacts {
        archives sourcesJar, javadocJar
}
uploadArchives {
    repositories {
        mavenDeployer {
			
            name = 'macgyver-bintray'

			if (!project.ext.has('BINRAY_USER')) {
				project.ext['BINTRAY_USER']=System.getenv("BINTRAY_USER")
			}
			if (!project.ext.has('BINTRAY_KEY')) {
				project.ext['BINTRAY_KEY'] = System.getenv("BINTRAY_KEY")
			}
			
            repository(id: 'macgyver-bintray', url: 'https://api.bintray.com/maven/lendingclub/OSS/NeoRx'){
                authentication(userName: BINTRAY_USER, password: BINTRAY_KEY)
			}
            pom.project {
				
				name 'NeoRx'
				description 'REST Client for Neo4j'
				url 'https://github.com/LendingClub/NeoRx'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
                    }
                }
				scm {
					connection 'scm:git:git@github.com:LendingClub/neorx.git'
					developerConnection 'scm:git:git@github.com:LendingClub/neorx.git'
					url 'git@github.com:LendingClub/neorx.git'
				}
            	developers {
					developer {
						name 'Rob Schoening'
						email 'robschoening@gmail.com'
					}
				}
            }
        }
    }
}

jacocoTestReport {
    group = "reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }
    //classDirectories = fileTree(dir: 'build/classes/main', include: 'com/thc/**')
    //sourceDirectories = fileTree(dir: 'scr/java', include: 'com/thc/**')
    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
}


